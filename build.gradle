plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.palantir.git-version' version '0.5.2'
}

compileJava {
    options.compilerArgs = ['-proc:none', '-Xlint:all','-Werror','-Xdiags:verbose']
}
compileTestJava {
    options.compilerArgs = ['-proc:none', '-Xlint:all','-Werror','-Xdiags:verbose']
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://artifactory.broadinstitute.org/artifactory/libs-snapshot/" }
}

dependencies {
    implementation 'commons-io:commons-io:2.7'
    implementation 'commons-logging:commons-logging:1.2'
    api 'org.broadinstitute:gatk-native-bindings:1.0.0'
    api 'com.github.samtools:htsjdk:3.0.5'
    testImplementation 'org.testng:testng:7.5.1'
    testImplementation 'org.apache.commons:commons-lang3:3.13.0'
}

wrapper {
    gradleVersion = '8.0.2'
}

//===================================================================
// build
//===================================================================
final nativeBuildDir = "$buildDir/native"

task cmakeConfig(type: Exec) {
    // hide stdout, but print stderr
    standardOutput = new ByteArrayOutputStream()
    doFirst {mkdir nativeBuildDir}
    workingDir nativeBuildDir
    commandLine 'cmake', '-Wno-dev', projectDir
    inputs.files fileTree(projectDir) {include '**/CMakeLists.txt'}.files
    outputs.files "$nativeBuildDir/Makefile"
}

task cmakeBuild(type: Exec) {
    // hide stdout, but print stderr
    standardOutput = new ByteArrayOutputStream()
    workingDir nativeBuildDir
    commandLine 'make', 'install', '-j', Runtime.runtime.availableProcessors()
    // always run this task
    outputs.upToDateWhen {false}
}

task copyNativeLib(type: Copy) {
    from nativeBuildDir
    into "$buildDir/classes/java/main/com/intel/gkl/native"
    include '*.so'
    include '*.dylib'
}

task buildOnMac(type: Exec) {
    String mac_host = project.ext.properties.mac_host ? project.ext.properties.mac_host : "gkl-mac"
    workingDir '.'
    commandLine 'scripts/buildOnMac.sh', mac_host
    // TODO: restore running buildOnMac task always when uploading to Maven
    onlyIf {project.hasProperty('mac') || project.hasProperty('mac_host')}
}

compileJava.finalizedBy copyNativeLib
copyNativeLib.finalizedBy buildOnMac
copyNativeLib.dependsOn cmakeBuild
jar.dependsOn copyNativeLib
javadoc.dependsOn copyNativeLib
// TODO: clean up dependencies using Gradle 7+ dependency mechanism
compileTestJava.dependsOn copyNativeLib
cmakeBuild.dependsOn cmakeConfig
test.dependsOn copyNativeLib

//===================================================================
// test
//===================================================================

test {
    def longTestsEnabled = System.getProperty('long_tests')

    def additionalJvmArgs = []

    if (longTestsEnabled != null){
        additionalJvmArgs.add '-Xmx24G'
    }
    useTestNG {
    }
    // propagate system properties to test JVM
    systemProperties = System.getProperties()

    if (project.hasProperty('debug')) {
        jvmArgs('-verbose:jni', '-Xcheck:jni', '-XX:+RestoreMXCSROnJNICalls', *additionalJvmArgs)
    }
    else {
        jvmArgs('-Xcheck:jni', '-XX:+RestoreMXCSROnJNICalls', *additionalJvmArgs)
    }

    testLogging {
        if (!System.env.CI.toString().toBoolean()) {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }

    // always rerun tests
    outputs.upToDateWhen { false }
}

//===================================================================
// release
//===================================================================

final isRelease = project.hasProperty("release")
version = (isRelease ? gitVersion() : gitVersion() + "-SNAPSHOT").replaceAll(".dirty", "")

jar {
    archiveBaseName = "gkl"
    // include LICENSE file in jar
    from "LICENSE"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = "gkl"
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    archiveBaseName = "gkl"
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishToMavenLocal.doFirst  { println "Version: $version" }

publishing {
    publications {

        mavenJava(MavenPublication) {

            groupId = 'com.intel.gkl'
            artifactId = 'gkl'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Intel Genomics Kernel Library (GKL)'
                description = 'Genomics compute kernels optimized for Intel Architecture'
                url = 'https://github.com/Intel-HLS/GKL'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://opensource.org/license/apache-2-0'
                    }
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                    license {
                        name = 'BSD 3-Clause'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                    license {
                        name = 'Zlib License'
                        url = 'https://opensource.org/licenses/Zlib'
                    }
                }
                developers {
                    developer {
                        name = "Venkata Kranthi Kumar Dhanala"
                        email = "venkata.kranthi.kumar.dhanala@intel.com"
                    }
                    developer {
                        name = "Urszula Golowicz"
                        email = "urszula.golowicz@intel.com"
                    }
                    developer {
                        name = "Mateusz S. Nowak"
                        email = "mateusz.s.nowak@intel.com"
                    }
                    developer {
                        name = "Priya Vaidya"
                        email = "priya.n.vaidya@intel.com"
                    }
                }
                scm {
                    connection = 'scm:git@github.com:Intel-HLS/GKL.git'
                    developerConnection = 'scm:git@github.com:Intel-HLS/GKL.git'
                    url = 'scm:git@github.com:Intel-HLS/GKL.git'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.properties["sonatypeUsername"]
                password = project.properties["sonatypePassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { !project.version.endsWith('SNAPSHOT') }
}
